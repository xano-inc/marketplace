run.job "Claude AI -> Ask a Question" {
  main = {name: "Claude AI -> Ask a Question", input: {}}
  env = ["claude_api_key"]
}
---
// ## Function Documentation: Claude AI -> Ask a Question
// [Claude AI API documentation](https://docs.anthropic.com/en/home)
// 
// ## Overview
// This function sends a message request to the Claude AI API using the provided API key, prompt, and token limit. The function captures the response from the Claude API and returns it.
// 
// ## Inputs
// 
// 1. **claude_api_key** (registry/text) *Required*
//    - **Description:** The API key used to authenticate requests to the Claude AI API.
//    - **Example:** `claude_api_abc123`
// 
// 2. **prompt** (text) *Required*
//    - **Description:** The prompt or message that you want to send to Claude AI as role user.
//    - **Example:** `What is the capital of France?`
// 
// 3. **max_token** (integer) *Required*
//    - **Description:** The maximum number of tokens to be used for generating the response, Default - 1024.
//    - **Example:** `1024`
// 
// 4. **assistant_prompt** (text) *Optional*
//    - **Description:** The prompt or message that you want to send to Claude AI as role assistant. Default - As an enthusiastic assistant, provide clear and understandable responses.
//    - **Example:** `As an enthusiastic assistant, provide clear and understandable responses
// 
// ## Function Stack
// 
// ### 1. Try / Catch Block
// 
// #### Group: Claude API Request
// 
//  1. **API Request to `https://api.anthropic.com/v1/messages`**
//     - **Purpose:** Sends the prompt to the Claude AI API with the API key, prompt, assistant prompt, and token limit.
//     - **Return Value:** The API response is stored as `api_response`.
//     - **Example:**
//     ``` json
//     {
//       "model": "claude-sonnet-4-20250514",
//       "message": [
//         {
//            "role": "user",
//            "content": "prompt"
//         },
//         {
//            "role": "assistant",
//            "content": "assistant_prompt"
//         }
//       ]
//     }
//     ```
// 
//  2. **Create Variable: `response`**
//     - **Variable:** `var: response = var: api_response.response`
//     - **Purpose:** Stores the API response, including the message generated by Claude AI.
// 
// ### 2 Error Handling (Catch)
// 
// 1. **Create Variable: `response`**
//    - **Purpose:** If the API call fails or returns an error, this block catches the error and provides an error message.
// 
// 
// ## Response
// 
// The function returns the result of the Claude AI message generation, including the generated text.
// 
// ### Success Response
// ```json
// {
//     "id": "msg_01H4x****",
//     "type": "message",
//     "role": "{role}",
//     "content": [
//         {
//             "type": "text",
//             "text": "Neural networks are a type of machine learning algorithm ..."
//         }
//     ],
//     "model": "claude-sonnet-4-20250514",
//     "stop_reason": "end_turn",
//     "stop_sequence": null,
//     "usage": {
//         "input_tokens": 23,
//         "output_tokens": 219
//     }
// }
// ```
// 
// ### Error Response
// 
// ```json
// {
//     "type": "error",
//     "error": {
//         "error": "authentication_error",
//         "message": "invalid x api key."
//     }
// }
// ```
// 
// ## Example
// 
// ### Input
// 
// ```json
//  {
//     "prompt": "{prompt}",
//     "max_tokens": "{max_token}",
//     "assistant_prompt": "{assistant_prompt}"
//  }
// ```
// 
// ### Output
// ```json
// {
//     "id": "msg_01H4x****",
//     "type": "message",
//     "role": "{role}",
//     "content": [
//         {
//             "type": "text",
//             "text": "Neural networks are a type of machine learning algorithm ..."
//         }
//     ],
//     "model": "claude-sonnet-4-20250514",
//     "stop_reason": "end_turn",
//     "stop_sequence": null,
//     "usage": {
//         "input_tokens": 23,
//         "output_tokens": 219
//     }
// }
// ```
function "Claude AI -> Ask a Question" {
  input {
    // User prompt to be asked from AI
    text prompt?="What is the most popular pizza topping?" filters=trim
  
    // Maximum number of tokens that can be used in single transaction
    int max_tokens?=1024
  
    // Prompt for the assistant 
    text assistant_prompt?="As an enthusiastic assistant, provide clear and understandable responses." filters=trim
  }

  stack {
    try_catch {
      try {
        // Claude API request
        group {
          stack {
            api.request {
              url = "https://api.anthropic.com/v1/messages"
              method = "POST"
              params = {}
                |set:"model":"claude-sonnet-4-20250514"
                |set:"max_tokens":$input.max_tokens
                |set:"messages":([]
                  |push:({}
                    |set:"role":"user"
                    |set:"content":$input.prompt
                  )
                  |push:({}
                    |set:"role":"assistant"
                    |set:"content":$input.assistant_prompt
                  )
                )
              headers = []
                |push:("x-api-key: %s"|sprintf:$env.claude_api_key)
                |push:"anthropic-version: 2023-06-01"
                |push:"content-type: application/json"
            } as $api_response
          
            // Response variable
            var $response {
              value = $api_response.response.result
            }
          }
        }
      }
    
      catch {
        // Error Handling
        var $response {
          value = {}
            |set:"code":"ERROR"
            |set:"message":$error.message
        }
      }
    }
  }

  response = $var.response
}
